<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="UTF-8">
<meta name="viewport" content="width=device-width, initial-scale=1.0">
<title>bad - Bad-value support</title>
</head>
<body>
<h1>bad - Bad-value support</h1>

    Welcome to this tour of the bad value support in PDL
<p/>
    Each ndarray contains a flag - accessible via the badflag() method -
    which indicates whether:
<p/>
       the ndarray contains no bad values (flag equals 0)
       the ndarray *MAY* contain bad values (flag equals 1)
<p/>
    If the flag is set, then the routines (well, those that have been
    converted) will process these bad values correctly, otherwise they
    are ignored.
<p/>
    The code has been written so as to provide as little overhead as
    possible; therefore there should be almost no difference in the
    time it takes to process ndarrays which do not have their bad flag
    set.

<hr/>
create an ndarray
<pre>
    $x = byte(1,2,3);
    print( "Bad flag (x) == ", $x->badflag(), "\n" );
</pre>
set bad flag, even though all the data is good
<pre>
    $x->badflag(1);
    print( "Bad flag (x) == ", $x->badflag(), "\n" );
</pre>
note the bad flag is infectious
<pre>
    $y = 2 * $x;
    print( "Bad flag (y) == ", $y->badflag(), "\n\n" );
</pre>
<h4>Output</h4>
<pre>
Bad flag (x) == 0
Bad flag (x) == 1
Bad flag (y) == 1
</pre>

<hr/>
the badflag is also included in the state info of
ndarray
<p/>
<pre>
    $z = pdl(2,3); # just an ndarray without the badflag set
    print "   Type   Dimension        State          Mem\n";
    print "-------------------------------------------------\n";
    print "x ", $x->info("%-6T %-15D   %-5S  %12M"), "\n";
    print "y ", $y->info("%-6T %-15D   %-5S  %12M"), "\n";
    print "z ", $z->info("%-6T %-15D   %-5S  %12M"), "\n\n";
</pre>
<h4>Output</h4>
<pre>
   Type   Dimension        State          Mem
-------------------------------------------------
x Byte   D [3]             PB           0.00KB
y Byte   D [3]             PB           0.00KB
z Double D [2]             P            0.02KB
</pre>

<hr/>
<pre>
    print "No bad values:   $x\n";
</pre>
set the middle value bad
<pre>
    $x->setbadat(1);
</pre>
now print out
<pre>
    print "Some bad values: $x\n";
    print "b contains:      $y\n";
    $z = $x + $y;
    print "so x + y =       $z\n\n";
</pre>
<h4>Output</h4>
<pre>
No bad values:   [1 2 3]
Some bad values: [1 BAD 3]
b contains:      [2 4 6]
so x + y =       [3 BAD 9]
</pre>

<hr/>
The module <a href="https://metacpan.org/pod/PDL::Bad">PDL::Bad</a> contains a number of routines designed
to make using bad values easy.
<pre>
    print "x contains ", $x->nbad, " bad elements.\n";
    print "The bad value for type #",$x->get_datatype," is ",$x->badvalue,"\n";
    print "It is easy to find whether a value is good: ", isgood($x), "\n\n";
    print "or to remove the bad values\n";
    $x->inplace->setbadtoval(23);
    print "x = $x and \$x->badflag == ", $x->badflag, "\n\n";
</pre>
<h4>Output</h4>
<pre>
x contains 1 bad elements.
The bad value for type #1 is 255
It is easy to find whether a value is good: [1 0 1]

or to remove the bad values
x = [1 23 3] and $x->badflag == 0
</pre>

<hr/>
<pre>
    print "We can even label certain values as bad!\n";
    $x = sequence(3,3);
    $x = $x->setbadif( $x % 2 ); # unfortunately can not be done inplace
    print $x;
</pre>
<h4>Output</h4>
<pre>
We can even label certain values as bad!

[
 [  0 BAD   2]
 [BAD   4 BAD]
 [  6 BAD   8]
]
</pre>

<hr/>
the issue of how to cope with dataflow is not fully resolved. At
present, if you change the badflag of an ndarray, all its children
are also changed:
<pre>
    $x = sequence( byte, 2, 3 );
    $x = $x->setbadif( $x == 3 );
    $y = $x->slice("(1),:");
    print "y = $y\tbadflag = ", $y->badflag, "\n";
    $x->inplace->setbadtoval(3);
    print "y = $y\tbadflag = ", $y->badflag, "\n\n";
</pre>
<h4>Output</h4>
<pre>
y = [1 BAD 5]	badflag = 1
y = [1 3 5]	badflag = 0
</pre>

<hr/>
Note that "boolean" operators return a bad value if either of the
operands are bad: one way around this is to replace all bad values
by 0 or 1.
<pre>
    $x = sequence(3,3); $x = $x->setbadif( $x % 2 );
    print $x > 5;
    print setbadtoval($x > 5,0);  # set all bad values to false
</pre>
<h4>Output</h4>
<pre>
[
 [  0 BAD   0]
 [BAD   0 BAD]
 [  1 BAD   1]
]

[
 [0 0 0]
 [0 0 0]
 [1 0 1]
]
</pre>

<hr/>
One area that is likely to cause confusion is the return value from
comparison operators (e.g. all and any) when ALL elements are bad.
The bad value is returned; if used in boolean context this causes
an exception, since it is neither true nor false.
There is also the fact that the bad value need not relate to the
type of the input ndarray (due to internal conversion to an 'int +').
<pre>
    $x = ones(3); $x = $x->setbadif( $x == 1 );
    print "Any returns: ", any( $x > 2 ), "\n";
    print "which is the bad value of 'long' (", long->badvalue, ").\n";
    print "Whereas the bad value for \$x is: ", $x->badvalue, "\n";
</pre>
<h4>Output</h4>
<pre>
Any returns: BAD
which is the bad value of 'long' (-2147483648).
Whereas the bad value for $x is: -1.79769313486232e+308
</pre>

<hr/>
    Many of the 'core' routines have been converted to handle bad values.
    However, some (including most of the additional modules) have not,
    either because it does not make sense or it's too much work to do!
<p/>
    To find out the status of a particular routine, use the 'badinfo'
    command in perldl shell (this information is also included
    when you do 'help'), or the '-b' switch of pdldoc.

<hr/>
    This demo is just a bit of eye-candy to show bad values in action,
    and requires <a href="https://metacpan.org/pod/PDL::Graphics::Simple">PDL::Graphics::Simple</a> support in PDL. It makes use of
    the image of M51 kindly provided by the Hubble Heritage group at
    the Space Telescope Science Institute.
<p/>
    It also serves to demonstrate that you often don't need to change
    your code to handle bad values, as the routines may 'do it' for you.

<hr/>
read in the image ($m51file has been set up by this demo to
contain the location of the file)
<pre>
    $m51 = rfits $<a href="https://metacpan.org/pod/PDL::Demos::BAD_demo::m51file">PDL::Demos::BAD_demo::m51file</a>;
</pre>
display it
<pre>
    $just = { JUSTIFY => 1 };
    imag $m51, $just;
</pre>
These are used to create the next image
<pre>
    ( $nx, $ny ) = $m51->dims;
    $centre = [ $nx/2, $ny/2 ];
</pre>
<h4>Output</h4>

<img src="images/demos/bad/output-1.png"/>

<hr/>
now, let's mask out the central 40 pixels and display it
<pre>
    $masked = $m51->setbadif( $m51->rvals({CENTRE=>$centre}) < 40 );
</pre>
since imag auto-scales the output, the bad values are not displayed
<pre>
    imag $masked, $just;
</pre>
compare the statistics of the images
<pre>
    print "Original:\n", $m51->stats, "\n";
    print "Masked:\n",   $masked->stats, "\n";
</pre>
<h4>Output</h4>
<pre>
Original:
338.537994384766108.5258483886723254076184.4224624633789108.525482177734
Masked:
329.63650512695398.62339019775393214076177.468399047851698.6230392456055
</pre>
<img src="images/demos/bad/output-2.png"/>

<hr/>
let's filter it a little bit
<pre>
    use <a href="https://metacpan.org/pod/PDL::Image2D">PDL::Image2D</a>;
    $nb = 15;
    $filtered = med2d $masked, ones($nb,$nb), { Boundary => 'Truncate' };
</pre>
this is a model of the diffuse component of M51
<pre>
    imag $filtered, $just;
</pre>
<h4>Output</h4>

<img src="images/demos/bad/output-3.png"/>

<hr/>
unsharp masking, to bring out the small-scale detail
<pre>
    $unsharp = $masked - $filtered;
    imag $unsharp, $just;
</pre>
<h4>Output</h4>

<img src="images/demos/bad/output-4.png"/>

<hr/>
add on some contours showing the large scale structure of the galaxy
<pre>
    erase; $w = pgswin();
    $w->plot(with=>'image', $unsharp, with=>'contours', $filtered, $just);
</pre>
<h4>Output</h4>

<img src="images/demos/bad/output-5.png"/>
</body>
</html>
