<!DOCTYPE html>
<html lang="en">
<head>
<meta charset="UTF-8">
<meta name="viewport" content="width=device-width, initial-scale=1.0">
<title>cartography - Cartographic projections</title>
</head>
<body>
<h1>cartography - Cartographic projections</h1>

 This demo illustrates the <a href="https://metacpan.org/pod/PDL::Transform::Cartography">PDL::Transform::Cartography</a> module.
 Also you must have <a href="https://metacpan.org/pod/PDL::Graphics::Simple">PDL::Graphics::Simple</a> installed to run it.
<p/>
 <a href="https://metacpan.org/pod/PDL::Transform::Cartography">PDL::Transform::Cartography</a> includes a global earth vector coastline map
 and night and day world image maps, as well as the infrastructure for 
 transforming them to different coordinate systems.

<hr/>
Load the necessary modules
<pre>
    use <a href="https://metacpan.org/pod/PDL::Graphics::Simple">PDL::Graphics::Simple</a>;
    use <a href="https://metacpan.org/pod/PDL::Transform::Cartography">PDL::Transform::Cartography</a>;
</pre>
Get the vector coastline map (and a lon/lat grid), and load the Earth
RGB daytime image -- both of these are built-in to the module. The
coastline map is a set of (X,Y,Pen) vectors.
<pre>
    $coast = earth_coast()->glue( 1, scalar graticule(15,1) );
    print "Coastline data are a collection of vectors:  ",
             join("x",$coast->dims),"\n";
    $map = earth_image('day');
    print "Map data are RGB:   ",join("x",$map->dims),"\n\n";
</pre>
<h4>Output</h4>
<pre>
Coastline data are a collection of vectors:  3x27065
Map data are RGB:   2048x1024x3
</pre>

<hr/>
Map data are stored natively in Plate Carree format.
The image contains a FITS header that contains coordinate system info.
<pre>
  print "FITS HEADER INFORMATION:\n";
  for $_(sort keys %{$map->hdr}){
    next if(m/SIMPLE/ || m/HISTORY/ || m/COMMENT/);
    printf ("  %8s: %10s%s", $_, $map->hdr->{$_}, (++$i%3) ? "  " : "\n"); 
  }
  print "\n";
  $w = pgswin();
  $w->plot(with=>'fits', $map, {Title=>"NASA/MODIS Earth Map (Plate Carree)",J=>0});
</pre>
<h4>Output</h4>
<pre>
FITS HEADER INFORMATION:
    CDELT1: 0.17578125      CDELT2: 0.17578125      CDELT3: 0.666666666666667
    CRPIX1:     1024.5      CRPIX2:      512.5      CRPIX3:          1
    CRVAL1:          0      CRVAL2:          0      CRVAL3:          0
    CTYPE1:  Longitude      CTYPE2:   Latitude      CTYPE3:        RGB
    CUNIT1:    degrees      CUNIT2:    degrees      CUNIT3:      index
     NAXIS:          3      NAXIS1:       2048      NAXIS2:       1024
    NAXIS3:          3  
</pre>
<img src="images/demos/cartography/output-1.png"/>

<hr/>
The map data are co-aligned with the vector data, which can be drawn
on top of the window with the "with polylines" <a href="https://metacpan.org/pod/PDL::Graphics::Simple">PDL::Graphics::Simple</a>
plot type.  The clean_lines method breaks lines that pass over
the map's singularity at the 180th parallel.
<pre>
  $w->hold;
  $w->plot(with=>'polylines', $coast->clean_lines);
  $w->release;
</pre>
<h4>Output</h4>

<img src="images/demos/cartography/output-2.png"/>

<hr/>
There are a large number of map projections -- to list them all,
say "??cartography" in the perldl or pdl2 shell.  Here are four
of them:
<pre>
undef $w; # Close old window
$w = pgswin( size=>[8,6], multi=>[2,2] ) ;
sub draw {
 ($tx, $t, $px, @opt ) = @_;
 $w->plot(with=>'fits', $map->map( $tx, $px, @opt ),
   with=>'polylines', $coast->apply( $tx )->clean_lines(@opt),
   {Title=>$t, J=>1});
}
</pre>
(The "or" option specifies the output range of the mapping)
<pre>
draw( t_mercator,  "Mercator Projection",    [400,300] );
draw( t_aitoff,    "Aitoff / Hammer",        [400,300] );
draw( t_gnomonic,  "Gnomonic",               [400,300],{or=>[[-3,3],[-2,2]]} );
draw( t_lambert,   "Lambert Conformal Conic",[400,300],{or=>[[-3,3],[-2,2]]} );
</pre>
<h4>Output</h4>

<img src="images/demos/cartography/output-3.png"/>

<hr/>
You can create oblique projections by feeding in a different origin.
Here, the origin is centered over North America.
<pre>
draw( t_mercator(  o=>[-90,40] ), "Mercator Projection",    [400,300] );
draw( t_aitoff (   o=>[-90,40] ), "Aitoff / Hammer",        [400,300] );
draw( t_gnomonic(  o=>[-90,40] ), "Gnomonic",[400,300],{or=>[[-3,3],[-2,2]]} );
draw( t_lambert(   o=>[-90,40] ), "Lambert ",[400,300],{or=>[[-3,3],[-2,2]]} );
</pre>
<h4>Output</h4>

<img src="images/demos/cartography/output-4.png"/>

<hr/>
There are three main perspective projections (in addition to special
cases like stereographic and gnomonic projection): orthographic,
vertical, and true perspective.  The true perspective has options for
both downward-looking and aerial-view coordinate systems.
<pre>
draw( t_orthographic( o=>[-90,40] ), 
      "Orthographic",  [400,300]);
draw( t_vertical( r0=> (2 + 1), o=>[-90,40] ), 
      "Vertical (Altitude = 2 r_e)", [400,300]);
draw( t_perspective( r0=> (2 + 1), o=>[-90,40] ),
      "True Perspective (Altitude= 2 r_e)", [400,300]);
</pre>
Observer is 0.1 earth-radii above surface, lon 117W, lat 31N (over Tijuana).
view is 45 degrees below horizontal, azimuth -22 (338) degrees.
<pre>
draw( t_perspective( r0=> 1.1, o=>[-117,31], cam=>[-22,-45,0] ),
      "Aerial view of West Coast of USA", [400,300],
      {or=>[[-60,60],[-45,45]], method=>'linear'});
</pre>
<h4>Output</h4>

<img src="images/demos/cartography/output-5.png"/>

<hr/>
That concludes the basic cartography demo.  Numerous other transforms
are available.  
<p/>
Because PDL's cartographic transforms work within the Transform module
and are invertible, it's easy to use them both forwards and backwards.
In particular, the perspective transformation is useful for ingesting 
scientific image data of the Earth or other planets, and converting to
a map of the imaged body.
<p/>
Similarly, scanned images of map data can easily be converted into 
lat/lon coordinates or reprojected to make other projections. 
</body>
</html>
